package org.kmr2.decision.impl;

import org.drools.pmml_4_0.ModelMarker;
import org.drools.pmml_4_0.PMML4Field;

import org.drools.fipa.*;
import org.drools.base.DroolsQuery;

import org.drools.informer.Answer;
import org.drools.informer.Questionnaire;

import org.drools.informer.generator.FormRegistry;
import org.drools.informer.generator.annotations.Questionable;
import org.drools.informer.generator.annotations.QuestionMark;
import org.drools.informer.generator.annotations.AllowedAnswers;

import org.drools.interaction.*;
import org.kmr2.decision.*;



declare org.kmr2.decision.DxAction
end


declare AskAlcohol extends org.kmr2.decision.DxAction
@Questionable( label = "Ask for Alcohol Abuse", language = "EN" )

    descr           : String            = "Ask for alcohol abuse"

    question        : String            = "unknown"
    @QuestionMark( type = Question.QuestionType.TYPE_TEXT, label = " Do you abuse alcohol ? ", required = false )
    @AllowedAnswers( values = { "true=true", "false=false", "unknown=unknown"  } )

end


declare AskDeployment extends org.kmr2.decision.DxAction
@Questionable( label = "Ask for Number of Deployments", language = "EN" )

    descr           : String            = "Ask for number of deployments "

    deploys         : String
    @QuestionMark( type = Question.QuestionType.TYPE_TEXT, label = " How many times have you been deployed ? ", required = true )
    @AllowedAnswers( values = { "0=No deployments", "1=Just once", "2+=Two or more times"  } )

end





query isUseful( DxAction $act, Double $use )
    $use := Double() from Math.random()
end







rule "Execute_AskAlcohol I"
when
    DiagnosticGuideProcess( $dm : diagModel == "MockDiag", $steps : stages, status != "Complete" )
    DxDecision( this memberOf $steps, current == true, $acts : actions )
    AskAlcohol( question == "true", this memberOf $acts )
    ModelMarker( modelName == $dm )
then
    System.out.println("Our friend uses ALCOHOL");
    drools.getWorkingMemory().getWorkingMemoryEntryPoint("in_Alcohol").insert("yes");
    drools.getWorkingMemory().getWorkingMemoryEntryPoint("in_Deployments").insert("2+");
end

rule "Execute_AskAlcohol II"
when
    DiagnosticGuideProcess( $dm : diagModel == "MockDiag", $steps : stages, status != "Complete" )
    DxDecision( this memberOf $steps, current == true, $acts : actions )
    AskAlcohol( question == "false", this memberOf $acts )
    ModelMarker( modelName == $dm )
then
    System.out.println("Our friend uses ALCOHOL NOT");
    drools.getWorkingMemory().getWorkingMemoryEntryPoint("in_Alcohol").insert("no");
    drools.getWorkingMemory().getWorkingMemoryEntryPoint("in_Deployments").insert("0");
end


rule "AskAlcohol Frame axiom"
when
    DiagnosticGuideProcess( $dm : diagModel == "MockDiag", $steps : stages, status != "Complete" )
    DxDecision( $step : stage, this memberOf $steps, current == true, $acts : actions )
    DxDecision( stage == ($step - 1), current == false, $prevs : actions ) from $steps
    $curr : AskAlcohol( $q : question, this memberOf $acts )
    $prev : AskAlcohol( $old : question != $q ) from $prevs
then
    $curr.setQuestion( $old );
    update( $curr );
end







rule "Mark"
when
then
    insert( new ModelMarker( "MockDecision", "Post Traumatic Stress Disorder" ) );
end






rule "Add AskAlcohol"
salience 100
no-loop
when
    $dx  : DiagnosticGuideProcess( decModel == "MockDecision", $acts : actions not contains AskAlcohol, status != "Complete" )
then
    System.out.println( " Linking' Ask Alcohol action " );
    $acts.add( AskAlcohol.class );
    FormRegistry.register( AskAlcohol.class );
end


rule "Add AskDeployment"
salience 100
no-loop
when
    $dx  : DiagnosticGuideProcess( decModel == "MockDecision", $acts : actions not contains AskDeployment, status != "Complete" )
then
    $acts.add( AskDeployment.class );
    FormRegistry.register( AskDeployment.class );
end

















rule "Close step"
salience 10
when
    $dx     : DiagnosticGuideProcess( $id : id, $steps : stages, $num : stages.size(), status != "Complete" )
    $x      : DxStep( dxProcessId == $id, stage > 0, stage == $num )
    $prev   : DxDecision( this memberOf $steps, current == true )
then
    System.out.println( "Closing " + $prev );
    $prev.setCurrent( false );
    retract( $prev );
end


rule "Close step II"
salience 15
when
    $dx     : DiagnosticGuideProcess( $id : id, $steps : stages, $num : stages.size(), status != "Complete" )
    $x      : DxStep( dxProcessId == $id, stage > 0, stage == $num )
    $prev   : DxDecision( this memberOf $steps, current == true, $acts : actions )
    $act    : DxAction( ) from $acts
//    $tkt    : InteractionTicket( id == $formId )
then
    System.out.println( "Killing orphan " + $act );
    retract( $act );
//    retract( $tkt );
end



rule "Close last step"
salience 10
when
    $dx     : DiagnosticGuideProcess( $id : id, $steps : stages, status == "Complete" )
    $prev   : DxDecision( this memberOf $steps, current == true )
then
    System.out.println( "Closing LASR" + $prev );
    $prev.setCurrent( false );
    retract( $prev );
end

rule "Close last step II"
salience 15
when
    $dx     : DiagnosticGuideProcess( $id : id, $steps : stages, status == "Complete" )
    $prev   : DxDecision( this memberOf $steps, current == true, $acts : actions )
    $act    : DxAction( $formId : questionnaireId ) from $acts
//    $tkt    : InteractionTicket( id == $formId )
then
    System.out.println( "Killing LAST orphan " + $act );
    retract( $act );
//    retract( $tkt );
end














rule "Create step"
no-loop
when
    $dx     : DiagnosticGuideProcess( $id : id, $steps : stages, $num : stages.size(), status != "Complete" )
    $x      : DxStep( dxProcessId == $id, stage == $num )
then
    System.out.println( "Create step " );
    DxDecision step = new DxDecision( java.util.UUID.randomUUID().toString() );
        step.setStage( $steps.size() );
        step.setCurrent( true );
        step.setDescr( "Stage " + step.getStage() );
        step.setActions( new java.util.ArrayList() );
    $steps.add( step );
    update( $dx );
    insertLogical( step );
end












rule "Initialize action forms"
salience -100
when
    DroolsQuery( name == "ticket", $args : elements )
    not InteractionTicket( id == $args[1] )
then
    String ticketId = $args[1].toString();
    InteractionTicket ticket = new InteractionTicket( ticketId,
                                                      new java.util.ArrayList(),
                                                      null );

    ticket.getInteractions().add( new Interaction( $args[0].toString(), null, new java.util.ArrayList() ) );
    System.out.println( "Created " + ticket + " on the fly");
    insert( ticket );
end


rule "Clone Actions"
salience -5
no-loop
when
    $dx     : DiagnosticGuideProcess( $id : id, $acts : actions, actions.size() > 0, $steps : stages, status != "Complete" )
    $x      : DxStep( dxProcessId == $id, $n : stage )
    $step   : DxDecision( stage == $n, this memberOf $steps, $childz : actions, current == true )
    $class  : Class( $className : name ) from $acts
              not DxAction( this memberOf $childz, class == $class )
    $tid    : String() from java.util.UUID.randomUUID().toString()
              ticket( $className, $tid, $ticket ; )
              formMarker( $tid, $className, $formId ; )
              $act : DxAction( questionnaireId == $formId, utility < 0.0 )
              ?isUseful( $act, $util ; )
then
    System.out.println( "Configuring action ");
        $act.setUtility( $util );
        $act.setStatus( "Not Started" );
        $childz.add( $act );
        update( $step );
        update( $act );
end



rule "Synch IDs"
when
    $act : DxAction( $qid : questionnaireId, $currNq : numQuestions )
    Questionnaire( id == $qid, $nq : numAvailableItems != $currNq )
then
    System.out.println( "Set action n. questions to" + $nq );
    $act.setNumQuestions( $nq );
end





rule "Update Diagnosis I"
when
    $dx  : DiagnosticGuideProcess( $mid : diagModel, status != "Complete" )
    $fld : PMML4Field( context == $mid, name == "Diag" )
then
    System.out.println( "Model diag " + $fld );
    DiagValue val = don( $fld, DiagValue.class, false );
end

rule "Update Diagnosis II"
no-loop
when
    $dx     : DiagnosticGuideProcess( $id : id, $mid : diagModel, $steps : stages, status != "Complete" )
    $x      : DxStep( dxProcessId == $id, $n : stage )
    $step   : DxDecision( stage == $n, this memberOf $steps, current == true, $curr : diseaseProbability )
              DiagValue( context == $mid, $val : value )
then
    int p = + (int) Math.round( $val * 100 );
    System.err.println(" New estimate is " + p );
    $step.setDiseaseProbability( p ) ;
    update( $step );
end


rule "Update Severity : Low"
when
    $dx : DxDecision( $curr : diseaseProbability <= 25)
then
    $dx.setSeverity( "Low" );
    $dx.setResponse( "The estimated disease probability is Low (" + $curr + ")" );
end

rule "Update Severity : Average"
when
    $dx : DxDecision( $curr : diseaseProbability > 25 && <= 50 )
then
    $dx.setSeverity( "Average" );
    $dx.setResponse( "The estimated disease probability is Average (" + $curr + ")" );
end

rule "Update Severity : High"
when
    $dx : DxDecision( $curr : diseaseProbability > 50 && <= 75)
then
    $dx.setSeverity( "High" );
    $dx.setResponse( "The estimated disease probability is High (" + $curr + ")" );
end

rule "Update Severity : Very High"
when
    $dx : DxDecision( $curr : diseaseProbability > 75)
then
    $dx.setSeverity( "Very High" );
    $dx.setResponse( "The estimated disease probability is Very High (" + $curr + ")" );
end









rule "Update Utility : Low"
when
    $dx : DxAction( $curr : utility <= 0.25)
then
    $dx.setUtilityLevel( "Low" );
end

rule "Update Utility : Normal"
when
    $dx : DxAction( $curr : utility > 0.25  )
then
    $dx.setUtilityLevel( "Normal" );
end


rule "Find most useful"
salience -100
when
    $step   : DxDecision( current == true, $acts : actions )
    $dx     : DxAction( this memberOf $acts, $u : utility )
              not DxAction( this != $dx, this memberOf $acts, utility > $u )
then
    $dx.setUtilityLevel( "Suggested" );
end














rule "Remove step marker"
salience -9999
when
    $x : DxStep()
then
    retract( $x );
end



