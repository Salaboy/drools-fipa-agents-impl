package org.drools.test;


import org.drools.fipa.body.content.Action;
import org.kmr2.decision.*;

import org.drools.io.internal.InternalResource;
import java.util.List;
import java.util.ArrayList;

import org.drools.pmml_4_0.ModelMarker;



rule "Action_Request : startDiagnosticGuideProcess"
when
    $a : Action( actionName == "startDiagnosticGuideProcess",
                 $provId      : this["providerId"],
                 $decModelId  : this["decModelId"],
                 $diagModelId : this["diagModelId"],
                 $patientId   : this["patientId "]
                )
    not  DiagnosticGuideProcess( decModel == $decModelId, diagModel == $diagModelId, status != "Complete" )
then
    DiagnosticGuideProcess dx = new DiagnosticGuideProcess();
        dx.setId( java.util.UUID.randomUUID().toString() );
        dx.setStatus( "Started" );
        dx.setDecModel( (String) $decModelId );
        dx.setDiagModel( (String) $diagModelId );
        dx.setSteps( new java.util.LinkedList() );
        dx.setActions( new java.util.HashSet() );
    insert( dx );
    retract( $a );

    insert( new DxStep( dx.getId(), 0 ) );
end


rule "Load Diagnostic Model"
when
    DiagnosticGuideProcess( status == "Started", $dxModel : diagModel );
    not   ModelMarker( modelName == $dxModel )
    $rs : InternalResource( name == $dxModel )
then
    manager.addResource( $dxModel, $rs );
end

rule "Load Decision Model"
when
    $dx : DiagnosticGuideProcess( status == "Started", $decModel : decModel );
    not   ModelMarker( modelName == $decModel )
    $rs : InternalResource( name == $decModel, $disease : description )
then
    System.out.println(" LOADING DECISION ");
    manager.addResource( $decModel, $rs );
    $dx.setDisease( $disease );
end





query startDiagnosticGuideProcess( String $providerId, String $decModelId, String $diagModelId, String $patientId, String $return )
    DiagnosticGuideProcess( decModel == $decModelId, diagModel == $diagModelId, $return := id )
end
