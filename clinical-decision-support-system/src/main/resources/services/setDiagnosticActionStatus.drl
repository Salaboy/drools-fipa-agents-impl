
import org.drools.fipa.body.content.Action;

import org.drools.informer.*;
import org.drools.informer.xml.ChangeCollector;
import org.drools.informer.presentation.*;

import org.kmr2.decision.*;




rule "Action_Request : setDiagnosticActionStatus - Complete"
when
    $a  : Action( actionName == "setDiagnosticActionStatus",
                  $userId       : this["userId"],
                  $patientId    : this["patientId"],
                  $processId    : this["dxProcessId"],
                  $actionId     : this["actionId"],
                  $status       : this["status"] == "Complete"
                  )
    $dx : DxAction( actionId == $actionId, status == "Started" || == "Committed" )
then
    $dx.setStatus( (String) $status );
    $dx.setStatusUpdated( new java.text.SimpleDateFormat().format( new java.util.Date() ) );
    update($dx);
end

rule "Action_Request : setDiagnosticActionStatus - Started"
when
    $a  : Action( actionName == "setDiagnosticActionStatus",
                  $userId       : this["userId"],
                  $patientId    : this["patientId"],
                  $processId    : this["dxProcessId"],
                  $actionId     : this["actionId"],
                  $status       : this["status"] == "Started"
                  )
    $dx : DxAction( actionId == $actionId, status == "Not Started" )
then
    $dx.setStatus( (String) $status );
    $dx.setStatusUpdated( new java.text.SimpleDateFormat().format( new java.util.Date() ) );
    update($dx);
end

rule "Action_Request : setDiagnosticActionStatus - Committed"
when
    $a  : Action( actionName == "setDiagnosticActionStatus",
                  $userId       : this["userId"],
                  $patientId    : this["patientId"],
                  $processId    : this["dxProcessId"],
                  $actionId     : this["actionId"],
                  $status       : this["status"] == "Committed"
                  )
    $dx : DxAction( actionId == $actionId, status == "Started" )
then
    $dx.setStatus( (String) $status );
    $dx.setStatusUpdated( new java.text.SimpleDateFormat().format( new java.util.Date() ) );
    update($dx);
end

rule "Action_Request : setDiagnosticActionStatus - Not Started"
when
    $a  : Action( actionName == "setDiagnosticActionStatus",
                  $userId       : this["userId"],
                  $patientId    : this["patientId"],
                  $processId    : this["dxProcessId"],
                  $actionId     : this["actionId"],
                  $status       : this["status"] == "Not Started"
                  )
    $dx : DxAction( actionId == $actionId, status == "Started" )
then
    $dx.setStatus( (String) $status );
    $dx.setStatusUpdated( new java.text.SimpleDateFormat().format( new java.util.Date() ) );
    update($dx);
end


rule "Action_Request : setDiagnosticActionStatus"
salience -100
when
    $a  : Action( actionName == "setDiagnosticActionStatus" )
then
    retract( $a );
end

declare SetDxActionReturn
    actionId          : String          @key
    actionStatus      : String
    successStatus     : boolean         = true
    statusMessage     : String          = "(ok)"
end


rule "Init result"
when
 $a  : Action( actionName == "setDiagnosticActionStatus",
               $actionId     : this["actionId"]
              )
       not SetDxActionReturn( $actionId ; )
then
    insert( new SetDxActionReturn( (String) $actionId, null, false, "Result initialized but not set") );
end



rule "Link result"
no-loop
when
 $a  : Action( actionName == "setDiagnosticActionStatus",
               $actionId     : this["actionId"]
              )
       DxAction( actionId == $actionId, $status : status )
 $r  : SetDxActionReturn( $actionId ; )
then
    $r.setActionStatus( $status );
    $r.setSuccessStatus( true );
    $r.setStatusMessage( "(ok)" );
    update ( $r );
end



query setDiagnosticActionStatus( String $userId, String $patientId, String $processId, String $actionId, String $status, SetDxActionReturn $return )
    //DxAction( actionId == $actionId, $return := status )
    $return := SetDxActionReturn( actionId == $actionId )
end
