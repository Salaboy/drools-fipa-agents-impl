package org.drools.test;


import org.drools.fipa.body.content.Action;

import org.drools.pmml_4_0.ModelMarker;



rule "Action_Request : setRiskThreshold"
when
    $a : Action( actionName == "setRiskThreshold",
                 $userId    : this["userId"],
                 $patientId : this["patientId"],
                 $modelId   : this["modelId"],
                 $value     : this["threshold"]
               )
    $h : RiskThreshold( modelId == $modelId )
then
    retract( $a );
    $h.setThreshold( Integer.valueOf( (String) $value ) );
    update($h);
end

rule "Action_Request : setRiskThreshold II"
when
    $a : Action( actionName == "setRiskThreshold",
                 $userId    : this["userId"],
                 $patientId : this["patientId"],
                 $modelId   : this["modelId"],
                 $value     : this["threshold"]
               )
    not  RiskThreshold( modelId == $modelId )
then
    insert( new RiskThreshold( (String) $modelId, Integer.valueOf( (String) $value ) ) );
    retract( $a );
end






rule "Support Threshold"
when
    ModelMarker( $mid : modelName );
    not RiskThreshold( modelId == $mid )
    ?defaultRiskThreshold( $def ; )
then
    insert( new RiskThreshold( $mid, $def ) );
end

rule "Clean Threshold"
when
    $risk : RiskThreshold( $mid : modelId )
    not InternalResource( name == $mid )
then
    retract( $risk );
end




query setRiskThreshold( String $userId, String $patientId, String $modelId, String $threshold, int $return )
    RiskThreshold( $modelId, $return ; )
end
